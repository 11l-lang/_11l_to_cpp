F is_square(x)
   I x < 0
      R 0B
   V y = Int(sqrt(x))
   R y * y == x

F is_prime(x)
   I x <= 1
      R 0B
   E I x <= 3
      R 1B
   E I x % 2 == 0
      R 0B
   E
      L(i) (3 .. Int(sqrt(x))).step(2)
         I x % i == 0
            R 0B
      R 1B

F list_primality(n)
   V result = [1B] * (n + 1)
   result[0] = result[1] = 0B
   L(i) 0 .. Int(sqrt(n))
      I result[i]
         L(j) (i * i .< result.len).step(i)
            result[j] = 0B
   R result

F list_primes(n)
   R enumerate(list_primality(n)).filter((i, isprime) -> isprime).map((i, isprime) -> i)

F list_totients(n)
   V result = Array(0 .. n)
   L(i) 2 .< result.len
      I result[i] == i
         L(j) (i .< result.len).step(i)
            result[j] -= result[j] I/ i
   R result

F binomial(BigInt n, BigInt k)
   assert(k C BigInt(0) .. n)
   R factorial(n) I/ (factorial(k) * factorial(n - k))
